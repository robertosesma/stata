/*
  dtroc.dlg

*! VERSION 1.1.2  11may2018

*/

VERSION 12.0

INCLUDE _std_msmall
DEFINE _dlght 250
INCLUDE header
HELP hlp1, view("help dtroc")
RESET res1

DEFINE _x _lft
DEFINE _y 220
DEFINE _cx 100

DIALOG main, label("dtroc - ROC Analysis & Optimal Cutoff Point") tabtitle("Main")
BEGIN
	TEXT  		tx_ref 	_lft   _top 	150  	., label("Reference variable:")
	VARNAME		v_ref	_lft   _ms		120  	., label("Reference variable")

	TEXT  		tx_clas 250		_top 	150  	., label("Classification variable:")
	VARNAME		v_clas	@    	_ms		120  	., label("Classification variable")

	GROUPBOX	gb_st	_lft   +40		370    50, label("Study type")
	RADIO    	rb_cs   +10    +20      110		., first default(1) label("Cross-Sectional")
	RADIO    	rb_cc   +240   @        100		., last default(0) label("Case-Control")
	
	GROUPBOX	gb_nst	_lft  	175		370    60, label("Name of the study")
	EDIT		ed_nst	_ilft   _ms	 	350		., option(nst)
END


INCLUDE ifin

DIALOG opt, tabtitle("Options")
BEGIN
	TEXT  		tx_ic 	_lft   _top 	230  	., label("Code of illness group:") right
	EDIT		ed_ic	250   	@    	60		., numonly default(1) option(ic)
	TEXT  		tx_p 	_lft   _ms 		230  	., label("Prevalence of illness in the population (%):") right
	EDIT		ed_p	250    @    	60		., numonly label("Code of illness group") option(prev)
	TEXT  		tx_p2 	_lft   _ss 		230  	., label("(sample prevalence is used by default)") right
	TEXT  		tx_c 	_lft   _ms 		230  	., label("Ratio false negative/false positive cost:") right
	EDIT		ed_c	250    @    	60		., numonly default(1) option(cost)
	CHECKBOX   	cb_inc  _lft   _ms     	370		., label("Illness increases classification variable") default(1)
	CHECKBOX   	cb_det  _lft   +20     	370		., label("Show details on sens./spec. for each cutpoint and save results") default(0)  /*
					*/								onclickon(program activate) onclickoff(program deactivate)
	FILE		fl_usin	+18    _ms		350		., label("Browse...") default("dtroc_cutoff.dta")/*
					*/								dialogtitle("Save dtroc Cut-off Table to Stata Data File") /*
					*/								filter("Stata Data (*.dta)|*.dta") save option(using)
	CHECKBOX	cb_repl	 @	   _ss		250	  	., label("Replace dataset if it already exists") default(1)
	CHECKBOX   	cb_gr   _lft   _ms     	370		., label("Graph sensitivity and specificity depending on each cutpoint") default(0)
	
	INCLUDE _sp_level
END

PROGRAM activate
BEGIN
	call opt.fl_usin.enable
	call opt.cb_repl.enable
END

PROGRAM deactivate
BEGIN
	call opt.fl_usin.disable
	call opt.cb_repl.disable
END


PROGRAM command
BEGIN
	put "dtroc "
	require main.v_ref main.v_clas
	put main.v_ref " " main.v_clas
	put " " /program ifin_output
	beginoptions
		if main.rb_cs {
			put " st(cs)"
		}
		if main.rb_cc {
			put " st(cc)"
		}

		if !opt.ed_ic.isdefault() {
			optionarg opt.ed_ic
		}
		optionarg opt.ed_p
		if !opt.ed_c.isdefault() {
			optionarg opt.ed_c
		}
		
		if !opt.cb_inc {
			put " noinc"
		}
		if opt.cb_det {
			put " detail"
			if !opt.fl_usin.isdefault() {
				optionarg opt.fl_usin
			}
			if !opt.cb_repl {
				put " noreplace"
			}
		}
		if opt.cb_gr {
			put " graph"
		}
		
		INCLUDE _level_opt_pr
		
		optionarg main.ed_nst
	endoptions
END
