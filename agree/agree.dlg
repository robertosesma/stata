/*
  agree.dlg

*! VERSION 1.1.5  19dec2018

*/

VERSION 12.0

INCLUDE _std_msmall
INCLUDE header
HELP hlp1, view("help agree")
RESET res1

DEFINE _x _lft
DEFINE _y _top
DEFINE _cx 100
DEFINE _rb_wd	350
DEFINE _vinc	+220
DEFINE _y_nst 	150
DEFINE _gb_wd	370

DIALOG main, label("agree - Passing-Bablok & Bland-Altman methods") tabtitle("Main")
BEGIN
	TEXT  		tx_y 	_lft	_top	_vlx	., label("Y variable:") left
	TEXT  		tx_x 	_vinc 	_top   	_vlx	., label("X variable:") left
	VARNAME		vary	_lft   	_ss		_vlx 	., label("Y variable")
	VARNAME		varx	_vinc	@		_vlx 	., label("X variable")

	RADIO		rb_both	_lft	_ls		_rb_wd	., label("Passing-Bablok and Bland-Altman methods") first default(1) option(both)
	RADIO		rb_pb	_lft	_ss		_rb_wd	., label("Passing-Bablok estimation of regression line") middle option(pb)
	RADIO		rb_ba	_lft	_ss		_rb_wd	., label("Bland-Altman comparison of two measurements methods") last option(ba)

	GROUPBOX	gb_nst	_lft  	_y_nst	_gb_wd	65, label("Name of the study")
	EDIT		ed_nst	_ilft   _ms	 	_rb_wd		., option(nst)
END

INCLUDE byifin

DIALOG opt, tabtitle("Options")
BEGIN
	INCLUDE _sp_level

	GROUPBOX	gb_pb	_lft  	_xls    _gb_wd	70, label("Graphics")
	CHECKBOX	cb_reg	_ilft	_ss		_rb_wd	., label("Show regression line on Bland-Altman graphics") option(line)
    CHECKBOX	cb_ci	_ilft	_ss		_rb_wd	., label("Show confidence intervals on Passing-Bablok graphic") option(ci)
    GROUPBOX	gb_list	_lft  	_xls    _gb_wd	80, label("Passing-Bablok")
	CHECKBOX	cb_list	_ilft	_ss	    _rb_wd	., label("List input data. Identifier:") onclickon(program enableid) /*
    */                                              onclickoff(program disableid) option(list)
	VARNAME 	vid		_iilft	_ss		_vlx	., label("Identifier") option(id)
END

PROGRAM disableid
BEGIN
	call opt.vid.disable
END

PROGRAM enableid
BEGIN
	call opt.vid.enable
END


PROGRAM command
BEGIN
	INCLUDE _by_pr
	put "agree "
	require main.vary main.varx
	put main.vary " " main.varx
	INCLUDE _ifin_pr
	beginoptions
		option radio(main rb_both rb_pb rb_ba)

        option opt.cb_reg
        option opt.cb_ci
		option opt.cb_list
		optionarg opt.vid

		INCLUDE _level_opt_pr

		optionarg main.ed_nst
	endoptions
END
